buildscript {
    apply from: 'dependencies.gradle'

    repositories {
        google()
        jcenter()
        maven { url appRepositories.maven }
        maven { url appRepositories.jitpack }
    }

    dependencies {
        classpath appClasspath.gradle
        classpath appClasspath.kotlin_gradle_plugin
        classpath appClasspath.navigation_safe_args
    }
}

plugins {
    id "org.jlleitschuh.gradle.ktlint" version "9.4.1"
    id "io.gitlab.arturbosch.detekt" version "1.15.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url appRepositories.maven
        }
    }
}

apply from: 'dependencies.gradle'

ktlint {
    debug = true
    verbose = true
    android = false
}

detekt {
    failFast = true
    buildUponDefaultConfig = false
    input = files(
            "$projectDir/app/src/main/java",
            "$projectDir/core/src/main/java",
            "$projectDir/data/src/main/java"
    )
    reports {
        html.enabled = true
        xml.enabled = true
        txt.enabled = true
    }
}

// Groovy DSL
tasks.detekt.jvmTarget = app.java_version

task clean(type: Delete) {
    delete rootProject.buildDir
}

task sanityCheck(type: GradleBuild) {
    tasks = ['ktlintFormat', 'ktlintCheck', 'detekt']
    group = "verification"
    description = "Runs a local check before doing a manual unit test run."
}